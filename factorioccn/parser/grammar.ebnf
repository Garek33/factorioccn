@@grammar :: fccn
@@whitespace :: /[\t ]+/
@@eol_comments :: /#.*?$/

start = '\n'.{lines+:statement | () } $;
statement::Statement = input:wireset '->' ~ action:combinator '->' output:wireset ;
wireset = @+:wire [',' @+:wire] ;
wire = identifier;
combinator = arithmetic | decider ;

decider::Decider = left:argument op:logiop ~ right:argument ':' result:deciderout ;
deciderout = signal:identifier value:['(1)'] ;
logiop = '>' | '>=' | '=' | '!=' | '<=' | '<' ;

arithmetic::Arithmetic = result:identifier '=' ~ left:argument op:arithop right:argument ;
arithop = '+' | '-' | '*' | '/' | '%' | '**' | '>>' | '<<' | '&' | '|' | '^' ;

argument = identifier | number ;

@name
identifier = /[a-zA-Z_][0-9a-zA-Z_]*/ ;
number::int = /[0-9]+/ ;
